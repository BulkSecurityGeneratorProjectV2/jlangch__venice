/*   __    __         _
 *   \ \  / /__ _ __ (_) ___ ___ 
 *    \ \/ / _ \ '_ \| |/ __/ _ \
 *     \  /  __/ | | | | (_|  __/
 *      \/ \___|_| |_|_|\___\___|
 *
 *
 * Copyright 2017-2019 Venice
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
 
// -----------------------------------------------------------------------------
// Build commands
//
//   - Build the Venice jar for testing
//     $> ./gradlew shadowJar
//
//   - Build the Venice cheatsheet (HTML & PDF)
//     $> ./gradlew cheatsheet
//
//   - Build the Venice bundle for uploading to Maven
//     $> ./gradlew bundle
//
//   - Run the unit tests
//     $> ./gradlew test
// -----------------------------------------------------------------------------
// Modify the Venice version:
//   - gradle.properties
//   - pom.xml
//   - README.md
//   - src/main/java/com/github/jlangch/venice/Version
//   - doc/maven-central.svg
//   
// Get the Gradle wrapper (Venice currently uses Gradle 5.2.1)
//   See https://guides.gradle.org/creating-new-gradle-builds/
//
//   Create and init a gradle project:
//      $> mkdir test
//      $> cd test
//      $> gradle init
// -----------------------------------------------------------------------------

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
    options.compilerArgs += "-parameters"
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
    options.compilerArgs += "-parameters"
}

repositories {
    jcenter()
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

test {
    useJUnitPlatform {
    }
}

jar {
}

shadowJar {
    // https://github.com/johnrengelman/shadow/

    relocate 'io.vavr', 'org.repackage.io.vavr'
    relocate 'org.jline', 'org.repackage.org.jline'

    def buildTimestamp = new Date().format("yyyy-MM-dd'T'HH:mm:ss")
        
    manifest {
        attributes 'Main-Class': 'com.github.jlangch.venice.Launcher',
                   'Build-Date': buildTimestamp,
                   'Implementation-Vendor' : 'com.github.jlangch',
                   'Implementation-Title' : 'Venice',
                   'Implementation-Version' : "${version}"
    }
    
    archiveName = "venice-${version}.jar"
}

task cheatsheet(type: JavaExec, dependsOn: compileJava) {
    description="Generate cheatsheet"

    main = 'com.github.jlangch.venice.impl.docgen.DocGenerator'
    classpath = sourceSets.main.runtimeClasspath + sourceSets.main.compileClasspath
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    
    archiveName = "venice-${version}-sources.jar"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    
    archiveName = "venice-${version}-javadoc.jar"
}

task bundle(type: Exec, dependsOn: [clean, shadowJar, sourcesJar, javadocJar]) {
    workingDir 'build/libs'
    
    commandLine "/bin/sh", 
                "-c", 
                "cp ../../pom.xml ." + 
                  "; echo 'Signing artifacts...'" +
                  "; gpg2 -ab pom.xml" +
                  "; gpg2 -ab venice-${version}-javadoc.jar" +
                  "; gpg2 -ab venice-${version}-sources.jar" +
                  "; gpg2 -ab venice-${version}.jar" +
                  "; echo 'Bundling artifacts...'" +
                  "; jar -cvf bundle.jar" +
                         " pom.xml pom.xml.asc" +
                         " venice-${version}-javadoc.jar venice-${version}-javadoc.jar.asc" +
                         " venice-${version}-sources.jar venice-${version}-sources.jar.asc" +
                         " venice-${version}.jar venice-${version}.jar.asc" +
                  "; echo 'Bundling done.'"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dependencies {
    // unit tests
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.1'

    // required for json module unit tests
    testRuntimeOnly 'com.fasterxml.jackson.core:jackson-annotations:2.9.9'
    testRuntimeOnly 'com.fasterxml.jackson.core:jackson-core:2.9.9'
    testRuntimeOnly 'com.fasterxml.jackson.core:jackson-databind:2.9.9'
    testRuntimeOnly 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.9.9'

    // required for cheatsheet generation
    compileOnly 'org.antlr:ST4:4.0.8'
    compileOnly 'org.apache.commons:commons-text:1.2'
    compileOnly 'org.apache.commons:commons-lang3:3.7'
    compileOnly 'org.xhtmlrenderer:flying-saucer-core:9.1.18'
    compileOnly 'org.xhtmlrenderer:flying-saucer-pdf-openpdf:9.1.18'
    compileOnly 'com.github.librepdf:openpdf:1.2.14'

    // WebApp support
    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
 
    // runtime dependencies (repackaged)
    compile 'io.vavr:vavr:0.10.0'
    compile 'org.jline:jline:3.11.0'
}
