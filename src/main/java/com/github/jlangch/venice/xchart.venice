;; XChart

(import :org.knowm.xchart.BubbleChart)
(import :org.knowm.xchart.XYChart)
(import :org.knowm.xchart.PieChart)
(import :org.knowm.xchart.CategoryChart)
(import :org.knowm.xchart.BubbleSeries$BubbleSeriesRenderStyle)
(import :org.knowm.xchart.CategorySeries$CategorySeriesRenderStyle)
(import :org.knowm.xchart.PieSeries$PieSeriesRenderStyle)
(import :org.knowm.xchart.XYSeries$XYSeriesRenderStyle)
(import :org.knowm.xchart.XChartPanel)
(import :org.knowm.xchart.BitmapEncoder)
(import :org.knowm.xchart.BitmapEncoder$BitmapFormat)
    
(import :org.knowm.xchart.style.Styler)
(import :org.knowm.xchart.style.AxesChartStyler)
(import :org.knowm.xchart.style.Styler$LegendPosition)
(import :org.knowm.xchart.style.Styler$TextAlignment)
(import :org.knowm.xchart.style.PieStyler$AnnotationType)
(import :org.knowm.xchart.style.GGPlot2Theme)
(import :org.knowm.xchart.style.MatlabTheme)
(import :org.knowm.xchart.style.XChartTheme)
    
(import :org.knowm.xchart.style.markers.Circle)
(import :org.knowm.xchart.style.markers.Diamond)
(import :org.knowm.xchart.style.markers.None)
(import :org.knowm.xchart.style.markers.Square)
(import :org.knowm.xchart.style.markers.TriangleDown)
(import :org.knowm.xchart.style.markers.TriangleUp)
    
(import :org.knowm.xchart.style.lines.SeriesLines)
   
(import :java.io.FileOutputStream)
(import :java.io.File)
   
(import :java.lang.Runnable)

(import :java.awt.Color)
(import :java.awt.GridLayout)
   
(import :javax.swing.JPanel)
(import :javax.swing.JFrame)
(import :javax.swing.SwingUtilities)



;; ---------------------------------------------------------------------------
;; utils
;; ---------------------------------------------------------------------------

;; Example: (xchart/doto-cond              =>    (let [e expr]
;;             expr                          (when cond1 (my e call))
;;             cond1 (my call)               (when cond2 (my2 e call2)))
;;             cond2 (my2 call2))
(defmacro xchart/doto-cond 
   [expr & clauses]
   (let [pairs (partition 2 clauses)
         expr-sym (gensym "expr")]
       `(let [~expr-sym ~expr]
          ~@(map (fn [[cond clause]]
                      `(when ~cond
                           (~(first clause) ~expr-sym ~@(rest clause))))
                  pairs)
       ~expr-sym)))

;; Maps a function over the values of an associative collection.
(def xchart/map-vals
   (fn [f coll]
       (reduce-kv (fn [m k v] (assoc m k (f v))) {} coll)))
       
(def xchart/transpose-single
   (fn [acc k1 v1]
       (reduce-kv (fn [m k2 v2] (assoc-in m [k2 k1] v2))
                  acc v1)))

(def xchart/transpose-map
   (fn [series]
       (reduce-kv xchart/transpose-single {} series)))

(def xchart/extract-series
   (fn [keymap coll]
       (xchart/map-vals (fn [x] (map x coll)) keymap)))

(def xchart/normalize-group
   (fn [m]
       (let [sum (reduce + (vals m))]
            (xchart/map-vals (fn [x] (/ x sum)) m))))

(def xchart/normalize-categories
   (fn [m]
       (->> (xchart/transpose-map m)
            (map-vals normalize-group)
            xchart/transpose-map)))

(def xchart/assoc-in-nonexisting  
   (fn [m ks v]
       (cond->
          m
          (== (get-in m ks ::not-found) ::not-found)
          (assoc-in ks v))))

(def xchart/to-bytes
   (fn [chart type]
        (let [format (get xchart/bitmap-formats type)]
            (. :BitmapEncoder :getBitmapBytes chart format))))

(def xchart/to-bytes-with-dpi
   (fn [chart type dpi]
        (let [format (get xchart/bitmap-formats type)
              file (temp-file "xchart-", (get xchart/file-extension type))]
            (. :BitmapEncoder :saveBitmapWithDPI chart file format dpi)
            (slurp-temp-file file :binary true :remove true))))

(def xchart/swing-open-window
   (fn [chart title]
       (let [frame (. :JFrame :new title)
             closeOP (. :JFrame :EXIT_ON_CLOSE)]
            (. frame :setDefaultCloseOperation closeOP)
            (. frame :add (. :XChartPanel :new chart))
            (. frame :pack)
            (. frame :setVisible true))))

(def xchart/swing-view-chart
   (fn [chart title]
       (. :SwingUtilities :invokeLater
               (proxify :Runnable { :run (fn [] (xchart/swing-open-window chart title)) }))))
                       
 



;; ---------------------------------------------------------------------------
;; constants
;; ---------------------------------------------------------------------------

(def xchart/colors
   { :blue       (. :Color :BLUE)
     :black      (. :Color :BLACK)
     :cyan       (. :Color :CYAN)
     :dark-gray  (. :Color :DARK_GRAY)
     :gray       (. :Color :GRAY)
     :green      (. :Color :GREEN)
     :light-gray (. :Color :LIGHT_GRAY)
     :magenta    (. :Color :MAGENTA)
     :orange     (. :Color :ORANGE)
     :pink       (. :Color :PINK)
     :red        (. :Color :RED)
     :white      (. :Color :WHITE)
     :yellow     (. :Color :YELLOW) })

(def xchart/strokes
   { :none      (. :SeriesLines :NONE)
     :solid     (. :SeriesLines :SOLID)
     :dash-dot  (. :SeriesLines :DASH_DOT)
     :dash-dash (. :SeriesLines :DASH_DASH)
     :dot-dot   (. :SeriesLines :DOT_DOT) })
    
(def xchart/markers
   { :circle        (. :Circle :new)
     :diamond       (. :Diamond :new)
     :none          (. :None :new)
     :square        (. :Square :new)
     :triangle-up   (. :TriangleUp :new)
     :triangle-down (. :TriangleDown :new) })    

(def xchart/xy-render-styles
   { :area    (. :XYSeries$XYSeriesRenderStyle :Area)
     :scatter (. :XYSeries$XYSeriesRenderStyle :Scatter)
     :line    (. :XYSeries$XYSeriesRenderStyle :Line) })

(def xchart/pie-render-styles
   { :pie   (. :PieSeries$PieSeriesRenderStyle :Pie)
     :donut (. :PieSeries$PieSeriesRenderStyle :Donut) })
    
(def xchart/pie-annotation-types
   { :label                (. :PieStyler$AnnotationType :Label)
     :label-and-percentage (. :PieStyler$AnnotationType :LabelAndPercentage)
     :percentage           (. :PieStyler$AnnotationType :Percentage) })

(def xchart/category-render-styles
   { :area    (. :CategorySeries$CategorySeriesRenderStyle :Area)
     :bar     (. :CategorySeries$CategorySeriesRenderStyle :Bar)
     :line    (. :CategorySeries$CategorySeriesRenderStyle :Line)
     :scatter (. :CategorySeries$CategorySeriesRenderStyle :Scatter)
     :stick   (. :CategorySeries$CategorySeriesRenderStyle :Stick) })

(def xchart/bubble-render-styles
   { :round (. :BubbleSeries$BubbleSeriesRenderStyle :Round) })

(def xchart/text-alignments
   { :centre (. :Styler$TextAlignment :Centre)
     :left   (. :Styler$TextAlignment :Left)
     :right  (. :Styler$TextAlignment :Right) })

(def xchart/legend-positions
   { :inside-n  (. :Styler$LegendPosition :InsideN)
     :inside-ne (. :Styler$LegendPosition :InsideNE)
     :inside-nw (. :Styler$LegendPosition :InsideNW)
     :inside-se (. :Styler$LegendPosition :InsideSE)
     :inside-sw (. :Styler$LegendPosition :InsideSW)
     :outside-e (. :Styler$LegendPosition :OutsideE) })

(def xchart/themes
   { :ggplot2 (. :GGPlot2Theme :new)
     :matlab  (. :MatlabTheme :new)
     :xchart  (. :XChartTheme :new) }) 
 
(def xchart/bitmap-formats
   { :png  (. :BitmapEncoder$BitmapFormat :PNG)
     :gif  (. :BitmapEncoder$BitmapFormat :GIF)
     :bmp  (. :BitmapEncoder$BitmapFormat :BMP)
     :jpg  (. :BitmapEncoder$BitmapFormat :JPG) })
 
(def xchart/file-extension
   { :png  ".png"
     :gif  ".gif"
     :bmp  ".bmp"
     :jpg  ".jpg" })
 
 
  
;; ---------------------------------------------------------------------------
;; chart styling
;; ---------------------------------------------------------------------------

(def xchart/set-legend!
   (fn [styler
        {:keys [background-color border-color font padding
                position series-line-length visible?]}]
       (xchart/doto-cond
          styler
          background-color      (. :setLegendBackgroundColor (get xchart/colors background-color background-color))
          border-color          (. :setLegendBorderColor (get xchart/colors border-color border-color))
          font                  (. :setLegendFont font)
          padding               (. :setLegendPadding padding)
          position              (. :setLegendPosition (get xchart/legend-positions position))
          series-line-length    (. :setLegendSeriesLineLength series-line-length)
          (not (nil? visible?)) (. :setLegendVisible visible?))))

(def xchart/set-chart-title-style!
   (fn [styler
        {:keys [box font padding visible?]}]
       (let [ box-background-color (:background-color box)
              box-border-color (:color box)
              box-visible? (:visible? box) ]
            (xchart/doto-cond
               styler
               box-background-color      (. :setChartTitleBoxBackgroundColor (get xchart/colors box-background-color box-background-color))
               box-border-color          (. :setChartTitleBoxBorderColor (get xchart/colors box-border-color box-border-color))
               (not (nil? box-visible?)) (. :setChartTitleBoxVisible box-visible?)
               font                      (. :setChartTitleFont font)
               padding                   (. :setChartTitlePadding padding)
               (not (nil? visible?))     (. :setChartTitleVisible visible?)))))

(def xchart/set-chart-style!
   (fn [styler
        {:keys [background-color font-color padding title]}]
       (xchart/doto-cond
          styler
          background-color (. :setChartBackgroundColor (get xchart/colors background-color background-color))
          font-color       (. :setChartFontColor (get xchart/colors font-color font-color))
          padding          (. :setChartPadding padding)
          title            (xchart/set-chart-title-style! title))))

(def xchart/set-plot-style!
   (fn [styler
        {:keys [background-color border-color border-visible? content-size]}]
       (xchart/doto-cond
          styler
          background-color             (. :setPlotBackgroundColor (get xchart/colors background-color background-color))
          border-color                 (. :setPlotBorderColor (get xchart/colors border-color border-color))
          (not (nil? border-visible?)) (. :setPlotBorderVisible border-visible?)
          content-size                 (. :setPlotContentSize content-size))))

(def xchart/set-series-style!
   (fn [styler series]
       (let [series-colors  (. styler :getSeriesColors)
             series-lines   (. styler :getSeriesLines)
             series-markers (. styler :getSeriesMarkers)
             series         (into [] series)]
          (dotimes [i (count series)]
              (let [{:keys [color stroke marker]} (nth series i)]
                   (when color
                         (update! series-colors i (fn [x] (get xchart/colors color color))))
                   (when stroke
                         (update! series-lines i (fn [x] (get xchart/strokes stroke stroke))))
                   (when marker
                         (update! series-markers i (fn [x] (get xchart/markers marker marker))))))
          (. styler :setSeriesColors series-colors)
          (. styler :setSeriesLines series-lines)
          (. styler :setSeriesMarkers series-markers))))

(def xchart/set-default-style!
   (fn [styler {:keys [annotations-font annotations? chart plot legend series]}]
       (xchart/doto-cond
          styler
          annotations-font          (. :setAnnotationsFont annotations-font)
          (not (nil? annotations?)) (. :setHasAnnotations (annotations?)
          chart                     (xchart/set-chart-style! chart)
          legend                    (xchart/set-legend! legend)
          plot                      (xchart/set-plot-style! plot)
          series                    (xchart/set-series-style! series)))))

(def xchart/set-axis-ticks!
   (fn [styler {:keys [labels marks padding visible? line-visible?]}]
       (let [{:keys [color font]} labels]
          (xchart/doto-cond
             styler
             color (. :setAxisTickLabelsColor (get xchart/colors color color))
             font  (. :setAxisTickLabelsFont font)))
       (let [{:keys [length color stroke visible?]} marks]
          (xchart/doto-cond
             styler
             length                (. :setAxisTickMarkLength length)
             color                 (. :setAxisTickMarksColor (get xchart/colors color color))
             stroke                (. :setAxisTickMarksStroke (get xchart/strokes stroke stroke))
             (not (nil? visible?)) (. :setAxisTicksMarksVisible visible?)))
       (xchart/doto-cond
          styler
          padding                    (. :setAxisTickPadding padding)
          (not (nil? line-visible?)) (. :setAxisTicksLineVisible line-visible?)
          (not (nil? visible?))      (. :setAxisTicksVisible visible?))))

(def xchart/set-axis-title!
   (fn [styler {:keys [font visible? padding]}]
       (xchart/doto-cond
          styler
          font                  (. :setAxisTitleFont font)
          padding               (. :setAxisTitlePadding padding)
          (not (nil? visible?)) (. :setAxisTitleVisible visible?))))

(def xchart/set-axis-plot!
   (fn [styler {:keys [grid-lines margin tick-marks?]}]
       (let [{:keys [horizontal? vertical? visible? color stroke]} grid-lines]
          (xchart/doto-cond
             styler
             (not (nil? visible?))    (. :setPlotGridLinesVisible visible?)
             color                    (. :setPlotGridLinesColor (get xchart/colors color color))
             stroke                   (. :setPlotGridLinesStroke (get xchart/strokes stroke stroke))
             (not (nil? horizontal?)) (. :setPlotGridHorizontalLinesVisible horizontal?)
             (not (nil? vertical?))   (. :setPlotGridVerticalLinesVisible vertical?)))
       (xchart/doto-cond
          styler
          margin                   (. :setPlotMargin margin)
          (not (nil? tick-marks?)) (. :setPlotTicksMarksVisible tick-marks?))))

(def xchart/set-x-axis-style!
   (fn [styler
        {:keys [label logarithmic? max min decimal-pattern
                tick-mark-spacing-hint ticks-visible? title-visible?]}]
       (let [{:keys [alignment rotation]} label]
          (xchart/doto-cond
             styler
             alignment (. :setXAxisLabelAlignment (get xchart/text-alignments alignment alignment))
             rotation  (. :setXAxisLabelRotation rotation)))
       (xchart/doto-cond
          styler
          decimal-pattern             (. :setXAxisDecimalPattern decimal-pattern)
          (not (nil? logarithmic?))   (. :setXAxisLogarithmic logarithmic?)
          max                         (. :setXAxisMax max)
          min                         (. :setXAxisMin min)
          tick-mark-spacing-hint      (. :setXAxisTickMarkSpacingHint tick-mark-spacing-hint)
          (not (nil? ticks-visible?)) (. :setXAxisTicksVisible ticks-visible?)
          (not (nil? title-visible?)) (. :setXAxisTitleVisible title-visible?))))

(def xchart/set-y-axis-style!
   (fn [styler
        {:keys [label logarithmic? max min decimal-pattern
                tick-mark-spacing-hint ticks-visible? title-visible?]}]
       (let [{:keys [alignment rotation]} label]
          (xchart/doto-cond
             styler
             alignment (. :setYAxisLabelAlignment (get xchart/text-alignments alignment alignment))))
          (xchart/doto-cond
             styler
             decimal-pattern             (. :setYAxisDecimalPattern decimal-pattern)
             (not (nil? logarithmic?))   (. :setYAxisLogarithmic logarithmic?)
             max                         (. :setYAxisMax max)
             min                         (. :setYAxisMin min)
             tick-mark-spacing-hint      (. :setYAxisTickMarkSpacingHint tick-mark-spacing-hint)
             (not (nil? ticks-visible?)) (. :setYAxisTicksVisible ticks-visible?)
             (not (nil? title-visible?)) (. :setYAxisTitleVisible title-visible?))))

(def xchart/set-axes-style!
   (fn [styler
        {:keys [axis error-bars-color plot x-axis y-axis
                date-pattern decimal-pattern locale marker timezone]}]
       (let [ebc error-bars-color 
             axis-ticks (:ticks axis)
             axis-title (:title axis)
             marker-size (:size marker)]
            (xchart/doto-cond
               styler
               axis-ticks      (xchart/set-axis-ticks! axis-ticks)
               axis-title      (xchart/set-axis-title! axis-title)
               date-pattern    (. :setDatePattern date-pattern)
               decimal-pattern (. :setDecimalPattern decimal-pattern)
               
               ;; The logic here is as follows: You can specify a colour for the error
               ;; bars. If the colour is :match-series, then the colour matches the series
               ;; colour, but if you specify something else, you cannot match the series!
               (and ebc (not= ebc :match-series)) (. :setErrorBarsColor (get xchart/colors ebc ebc))
               (and ebc (not= ebc :match-series)) (. :setErrorBarsColorSeriesColor false)
               (== ebc :match-series)             (. :setErrorBarsColorSeriesColor true)
               
               locale      (. :setLocale locale)
               marker-size (. :setMarkerSize marker-size)
               plot        (xchart/set-axis-plot! plot)
               timezone    (. :setTimezone timezone)
               x-axis      (xchart/set-x-axis-style! x-axis)
               y-axis      (xchart/set-y-axis-style! y-axis)))))

(def xchart/add-raw-series
   (fn [chart s-name x-data y-data error-bars]
       (if (some? error-bars)
           (. chart :addSeries s-name x-data y-data error-bars)
           (. chart :addSeries s-name x-data y-data))))

(def xchart/attach-default-font
   (fn [style-map]
       (if-let [font (:font style-map)]
          (-> style-map
              (dissoc style-map :font)
              (xchart/assoc-in-nonexisting [:axis :ticks :labels :font] font)
              (xchart/assoc-in-nonexisting [:axis :title :font] font)
              (xchart/assoc-in-nonexisting [:legend :font] font)
              (xchart/assoc-in-nonexisting [:annotations-font] font)
              (xchart/assoc-in-nonexisting [:chart :title :font] font))
          style-map)))       

 
 
 
;; ---------------------------------------------------------------------------
;; XY chart
;; ---------------------------------------------------------------------------

(def xchart/xy-chart-add-series!
   (fn [chart s-name data]
       (if (sequential? data)
           (apply xchart/add-raw-series chart s-name data)
           (let [{:keys [x y error-bars style]} data
                 {:keys [marker-color marker-type
                         line-color line-style line-width
                         fill-color show-in-legend? render-style]} style]
                (xchart/doto-cond
                   (if error-bars
                       (xchart/add-raw-series chart s-name x y error-bars)
                       (xchart/add-raw-series chart s-name x y))
                   render-style                 (. :setXYSeriesRenderStyle (get xchart/xy-render-styles render-style))
                   marker-color                 (. :setMarkerColor (get xchart/colors marker-color marker-color))
                   marker-type                  (. :setMarker (get xchart/markers marker-type marker-type))
                   line-color                   (. :setLineColor (get xchart/colors line-color line-color))
                   line-style                   (. :setLineStyle (get xchart/strokes line-style line-style))
                   line-width                   (. :setLineWidth line-width)
                   fill-color                   (. :setFillColor (get xchart/colors fill-color fill-color))
                   (not (nil? show-in-legend?)) (. :setShowInLegend show-in-legend?))))))

(def xchart/xy-chart
   (fn [series
        {:keys [width height title theme render-style]
         :or {width 640 height 500}
         :as styling}]
       (let [chart (. :XYChart :new width height)
             styling (xchart/set-legend! styling)]
          
          (xchart/doto-cond
             (. chart :getStyler)
             theme        (. :setTheme (get xchart/themes theme theme))
             render-style (. :setDefaultSeriesRenderStyle (get xchart/xy-render-styles render-style)))
             
          (docoll (fn [[s-name data]] (xchart/xy-chart-add-series! chart s-name data)) series)
             
          (doto (. chart :getStyler)
                (xchart/set-default-style! styling)
                (xchart/set-axes-style! styling))
                
          (xchart/doto-cond
              chart
              title (. :setTitle title)
              (-> styling :x-axis :title) (. :setXAxisTitle (-> styling :x-axis :title))
              (-> styling :y-axis :title) (. :setYAxisTitle (-> styling :y-axis :title))))))

 
 
;; ---------------------------------------------------------------------------
;; Area chart
;; ---------------------------------------------------------------------------
 
(def xchart/category-series-xs
   (fn [series]
       (->> (vals series)
            (mapcat (fn [m] (:x m)) )
            (into (set )))))

(def xchart/reorder-category-series
   (fn [{:keys [x y] :as series} x-order]
       (let [mapping (zipmap x y)]
            (assoc series
                   :x x-order
                   :y (mapv (fn [x] (get mapping x 0.0)) x-order)))))

(def xchart/normalize-category-series
   (fn [series-data]
       (cond (and (map? series-data)
                  (contains? series-data :x)
                  (contains? series-data :y)) series-data
                  
             (and (map? series-data)
                  (contains? series-data :content)) (-> (:content series-data)
                                                        (xchart/normalize-category-series)
                                                         ;; retain styling data:
                                                        (merge (dissoc series-data :content)))
             ;; Assuming keys are strings/vals
             (and (map? series-data)
                  (every? (comp not keyword?)
                          (keys series-data))) {:x (keys series-data)
                                                :y (vals series-data)}
             (sequential? series-data) {:x (first series-data)
                                        :y (second series-data)})))
 
(def xchart/normalize-category-series-map
   (fn [series-map x-order]
       (let [series-map (xchart/map-vals xchart/normalize-category-series series-map)
             x-order (into [] x-order)
             extra-xs (sort (difference (xchart/category-series-xs series-map)
                                        (into (set ) x-order)))
             x-order (into x-order extra-xs)]
          (xchart/map-vals (fn [x] (xchart/reorder-category-series x x-order)) series-map))))

(def xchart/category-add-series! 
   (fn [chart s-name data]
       (if (sequential? data)
          (apply xchart/add-raw-series chart s-name data)
          (let [{:keys [x y error-bars style]} data
                {:keys [marker-color marker-type
                        line-color line-style line-width
                        fill-color show-in-legend? render-style]} style]
             (xchart/doto-cond
                (if error-bars
                    (xchart/add-raw-series chart s-name x y error-bars)
                    (xchart/add-raw-series chart s-name x y))
                render-style                 (. :setChartCategorySeriesRenderStyle (get xchart/category-render-styles render-style))
                marker-color                 (. :setMarkerColor (get xchart/colors marker-color marker-color))
                marker-type                  (. :setMarker (get xchart/markers marker-type marker-type))
                line-color                   (. :setLineColor (get xchart/colors line-color line-color))
                line-style                   (. :setLineStyle (get xchart/strokes line-style line-style))
                line-width                   (. :setLineWidth line-width)
                fill-color                   (. :setFillColor (get xchart/colors fill-color fill-color))
                (not (nil? show-in-legend?)) (. :setShowInLegend show-in-legend?))))))
                 
(def xchart/category-chart*
   (fn [series
        {:keys [width height title theme render-style available-space-fill overlap? stacked?]
         :or {width 640 height 500}
         :as styling}]
   
       (let [chart (. :CategoryChart :new width height)
             styling (xchart/attach-default-font styling)]
     
          (docoll (fn [[s-name data]] (xchart/category-add-series! chart s-name data)) series)
       
          (xchart/doto-cond
             (. chart :getStyler)
             theme                 (. :setTheme (get xchart/themes theme theme))
             render-style          (. :setDefaultSeriesRenderStyle (get xchart/category-render-styles render-style))
             available-space-fill  (. :setAvailableSpaceFill available-space-fill)
             (not (nil? overlap?)) (. :setOverlapped overlap?)
             (not (nil? stacked?)) (. :setStacked stacked?))
     
          (doto (. chart :getStyler)
                (xchart/set-default-style! styling)
                (xchart/set-axes-style! styling))
     
          (xchart/doto-cond
             chart
             title (. :setTitle title)
             (-> styling :x-axis :title) (. :setXAxisTitle (-> styling :x-axis :title))
             (-> styling :y-axis :title) (. :setYAxisTitle (-> styling :y-axis :title))))))

(def xchart/category-chart
   (fn [series {:keys [x-axis series-order] :as styling}]
       (let [x-order (:order x-axis)
             normalized-map (xchart/normalize-category-series-map series x-order)
             extra-categories (->> (apply dissoc normalized-map series-order)
                                   (sort-by key))
             normalized-seq (concat (keep (fn [x] (find normalized-map x)) series-order) 
                                     extra-categories)]
     (xchart/category-chart* normalized-seq styling))))


     

;; ---------------------------------------------------------------------------
;; Bubble chart
;; ---------------------------------------------------------------------------

(def xchart/max-bubble-value 
   (fn [series]
       (reduce max (mapcat :bubble (vals series)))))

(def xchart/scale-bubbles
   (fn [series in-val out-val]
       (let [bubble-fn (fn [x] (* out-val (Math/sqrt (/ x in-val))))]
            (xchart/map-vals
               (fn [data] (update data :bubble (fn [x] (map bubble-fn x))))
               series))))

(def xchart/bubble-add-series! 
   (fn [chart s-name data]
       (if (sequential? data)
         (apply xchart/add-raw-series chart s-name data)
         (let [{:keys [x y bubble style]} data
               {:keys [marker-color marker-type
                       line-color line-style line-width
                       fill-color show-in-legend? render-style]} style]
           (xchart/doto-cond
              (xchart/add-raw-series chart s-name x y bubble)
              render-style                 (. :setBubbleSeriesRenderStyle (get xchart/bubble-render-styles render-style))
              line-color                   (. :setLineColor (get xchart/colors line-color line-color))
              line-style                   (. :setLineStyle (get xchart/strokes line-style line-style))
              line-width                   (. :setLineWidth line-width)
              fill-color                   (. :setFillColor (get xchart/colors fill-color fill-color))
              (not (nil? show-in-legend?)) (. :setShowInLegend show-in-legend?))))))
         
(def xchart/bubble-chart
   (fn [series
        {:keys [width height title theme render-style]
         :or {width 640 height 500}
         :as styling}]
          
       (let [chart (. :BubbleChart :new width height)
             styling (xchart/attach-default-font styling)]
       
          (docoll (fn [[s-name data]] (xchart/bubble-add-series! chart s-name data)) series)
           
          (xchart/doto-cond
             (. chart :getStyler)
             theme        (. :setTheme (get xchart/themes theme theme))
             render-style (. :setDefaultSeriesRenderStyle (get xchart/bubble-render-styles render-style)))
     
          (doto (. chart :getStyler)
                (xchart/set-default-style! styling)
                (xchart/set-axes-style! styling))
    
          (xchart/doto-cond
             chart
             title (. :setTitle title)
             (-> styling :x-axis :title) (. :setXAxisTitle (-> styling :x-axis :title))
             (-> styling :y-axis :title) (. :setYAxisTitle (-> styling :y-axis :title))))))
 
 
 
;; ---------------------------------------------------------------------------
;; Pie chart
;; ---------------------------------------------------------------------------

(def xchart/attach-default-annotation-distance
   (fn [styling]
       (if (and (== :donut (:render-style styling))
                (not (:annotation-distance styling)))
          (assoc styling :annotation-distance
                (- 1.0 (/ (:donut-thickness styling 0.33) 2)))
          styling)))

(def xchart/pie-chart-add-series!
   (fn [chart s-name data]
       (if (number? data)
           (. chart :addSeries s-name data)
           (let [{:keys [render-style fill-color show-in-legend?]} (:style data)
                 val (:value data)]
              (xchart/doto-cond
                 (. chart :addSeries s-name val)
                 render-style                 (. :setChartPieSeriesRenderStyle (get xchart/pie-render-styles render-style))
                 fill-color                   (. :setFillColor (get xchart/colors fill-color fill-color))
                 (not (nil? show-in-legend?)) (. :setShowInLegend show-in-legend?))))))           

(def xchart/pie-chart
   (fn [series
        {:keys [width height title circular? theme render-style annotation-distance
                start-angle draw-all-annotations? donut-thickness annotation-type]
         :or {width 640 height 500}
         :as styling}]
       (let [chart (. :PieChart :new width height)
             styling (-> styling
                         xchart/attach-default-font
                         xchart/attach-default-annotation-distance)
             annotation-distance (:annotation-distance styling)]
             
          (docoll (fn [[s-name data]] (xchart/pie-chart-add-series! chart s-name data)) series)
          
          (xchart/doto-cond
             (. chart :getStyler)
             theme                              (. :setTheme (get xchart/themes theme theme))
             render-style                       (. :setDefaultSeriesRenderStyle (get xchart/pie-render-styles render-style))
             (not (nil? circular?))             (. :setCircular circular?)
             (not (nil? draw-all-annotations?)) (. :setDrawAllAnnotations draw-all-annotations?)
             annotation-distance                (. :setAnnotationDistance annotation-distance)
             donut-thickness                    (. :setDonutThickness donut-thickness)
             start-angle                        (. :setStartAngleInDegrees start-angle)
             annotation-type                    (. :setAnnotationType (get xchart/pie-annotation-types annotation-type)))
             
          (xchart/set-default-style! (. chart :getStyler) styling)
                
          (xchart/doto-cond
              chart
              title (. :setTitle title)
              (-> styling :x-axis :title) (. :setXAxisTitle (-> styling :x-axis :title))
              (-> styling :y-axis :title) (. :setYAxisTitle (-> styling :y-axis :title))))))
 
