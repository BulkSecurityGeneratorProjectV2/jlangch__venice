;;;;   __    __         _
;;;;   \ \  / /__ _ __ (_) ___ ___
;;;;    \ \/ / _ \ '_ \| |/ __/ _ \
;;;;     \  /  __/ | | | | (_|  __/
;;;;      \/ \___|_| |_|_|\___\___|
;;;;
;;;;
;;;; Copyright 2017-2020 Venice
;;;;
;;;; Licensed under the Apache License, Version 2.0 (the "License");
;;;; you may not use this file except in compliance with the License.
;;;; You may obtain a copy of the License at
;;;;
;;;;     http://www.apache.org/licenses/LICENSE-2.0
;;;;
;;;; Unless required by applicable law or agreed to in writing, software
;;;; distributed under the License is distributed on an "AS IS" BASIS,
;;;; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
;;;; See the License for the specific language governing permissions and
;;;; limitations under the License.

;;;; GEOIP functions. Maps IP adresses to country and location (latitude,
;;;; longitude).
;;;;
;;;; E.g.: "91.223.55.1"  => ["91.223.55.1", "PL", "Poland", ["51.919438", "19.145136"]]
;;;;
;;;;
;;;; You can use Venice's :mercator extension module to create a world map with
;;;; the IP locations:
;;;;
;;;;      (do
;;;;        (load-module :mercator)
;;;;        (load-module :geiop)
;;;;
;;;;        (defn draw [format file locations]
;;;;          (-> (mercator/load-mercator-image)
;;;;              (mercator/draw-locations locations)
;;;;              (mercator/crop-image 400 600)
;;;;              (mercator/save-image format file))))
;;;;
;;;;       (def rx (geoip/resolver "resources/geoip.zip"
;;;;                               (geoip/download-country-gps-db)))
;;;;
;;;;       (->> ["91.223.55.1" "220.100.34.45" "167.120.90.10"]
;;;;            (map rx)
;;;;            (map fourth)
;;;;            (draw :png "./world-map.png")))
;;;;


(ns geoip)

(load-module :csv)


;; MaxMind GEO IP database
(def geoip/geolite-country-url "http://geolite.maxmind.com/download/geoip/database/GeoLite2-Country-CSV.zip")

(def geoip/ip4-file "GeoLite2-Country-Blocks-IPv4.csv")
(def geoip/ip6-file "GeoLite2-Country-Blocks-IPv6.csv")
(def geoip/country-file "GeoLite2-Country-Locations-en.csv")


;; Google country GPS data
;;   https://developers.google.com/public-data/docs/canonical/countries_csv
;;   https://github.com/google/dspl/tree/master/samples/google/canonical
;;   https://raw.githubusercontent.com/google/dspl/master/samples/google/canonical/countries.csv
(def geoip/google-country-url "https://raw.githubusercontent.com/google/dspl/master/samples/google/canonical/countries.csv")



;; (def rx (geoip/resolver "resources/geoip.zip" (geoip/download-country-gps-db)))
;; (rx "91.223.55.1") ; => ["91.223.55.1", "PL", "Poland", ["51.919438", "19.145136"]]
(defn geoip/resolver [geoip-zip location-csv]
  (let [rc (geoip/country-resolver geoip-zip)
        rl (geoip/location-resolver location-csv)]
    (memoize (fn resolve [ip]
               (let [i (rc ip)]
                 (when (some? i)
                   (cons ip (conj i (rl (first i))))))))))

;; (def rx (geoip/country-resolver "resources/geoip.zip"))
;; (rx "91.223.55.1") ; => ["PL", "Poland"]
(defn geoip/country-resolver [geoip-zip]
  (let [countries (geoip/parse-countries geoip-zip)
        ip4-data (geoip/parse-ip4 geoip-zip countries)
        ip6-data (geoip/parse-ip6 geoip-zip countries)]
    (memoize (fn resolve [ip]
                (cond
                  (geoip/ip4? ip) (geoip/find-ip-country ip ip4-data)
                  (geoip/ip6? ip) (geoip/find-ip-country ip ip6-data)
                  :else nil)))))

;; (def rx (geoip/location-resolver (geoip/download-country-gps-db)))
;; (rx "PL") ; => ["51.919438", "19.145136"]
(defn geoip/location-resolver [location-csv]
  (let [locations (geoip/parse-locations location-csv)]
    (memoize (fn resolve [country-iso]
                (get locations country-iso)))))

(defn geoip/download-country-ip-db-to-file [zip-file]
  (io/spit zip-file (geoip/download-country-ip-db)))

(defn geoip/download-country-ip-db []
  (io/download geoip/geolite-country-url :binary true))

(defn geoip/download-country-gps-db-to-file [csv-file]
  (io/spit csv-file (geoip/download-country-gps-db)))

(defn geoip/download-country-gps-db []
  (io/download geoip/google-country-url :binary false))

(defn- geoip/unzip-entry [zip name]
  (let [entry (->> (io/zip-list-entry-names zip)
                   (filter #(str/ends-with? % name))
                   (first))]
    (io/unzip (io/file zip) entry)))

(defn- geoip/parse-file [zip name]
  (-<> (geoip/unzip-entry zip name)
       (bytebuf-to-string <> :utf-8)
       (csv/read <>)))

(defn- geoip/parse-ip [zip file countries]
  (let [ip-data (->> (geoip/parse-file zip file)
                     (filter #(and (not (nil? (nth % 2)))
                                   (= "0" (nth % 4))
                                   (= "0" (nth % 5)))))]
    (if (some? countries)
      (map (juxt #(nth % 0) #(get countries (nth % 2))) ip-data) ; map network -> country-name
      (map (fn [x] { :id (nth x 1)
                     :network (nth x 0)
                     :country-id (nth x 2) }) ip-data))))

(defn- geoip/find-ip-country [ip ip-data]
  (some (fn [x] (if (cidr/in-range? ip (first x)) (second x) nil)) ip-data))

(defn- geoip/parse-locations [csv]
  (->> (csv/read csv)
       (map (juxt #(nth % 0) #(vector (nth % 1) (nth % 2))))  ; [iso, [lat, lon]]
       (into {})))

(defn geoip/ip4? [ip]
  (case (type ip)
    :venice.String         (str/contains? ip ".")
    :java.net.Inet4Address true
    :java.net.Inet6Address false
    false))

(defn geoip/ip6? [ip]
  (case (type ip)
    :venice.String         (str/contains? ip ":")
    :java.net.Inet4Address false
    :java.net.Inet6Address true
    false))

(defn geoip/parse-countries [zip]
  (->> (geoip/parse-file zip geoip/country-file)
       (map (juxt #(nth % 0) #(vector (nth % 4) (nth % 5)))) ; [id, [iso, name]]
       (into {})))

(defn geoip/parse-ip4
  ([zip] (geoip/parse-ip zip geoip/ip4-file nil))
  ([zip countries] (geoip/parse-ip zip geoip/ip4-file countries)))

(defn geoip/parse-ip6
  ([zip] (geoip/parse-ip zip geoip/ip6-file nil))
  ([zip countries] (geoip/parse-ip zip geoip/ip6-file countries)))
