;;;;   __    __         _
;;;;   \ \  / /__ _ __ (_) ___ ___
;;;;    \ \/ / _ \ '_ \| |/ __/ _ \
;;;;     \  /  __/ | | | | (_|  __/
;;;;      \/ \___|_| |_|_|\___\___|
;;;;
;;;;
;;;; Copyright 2017-2020 Venice
;;;;
;;;; Licensed under the Apache License, Version 2.0 (the "License");
;;;; you may not use this file except in compliance with the License.
;;;; You may obtain a copy of the License at
;;;;
;;;;     http://www.apache.org/licenses/LICENSE-2.0
;;;;
;;;; Unless required by applicable law or agreed to in writing, software
;;;; distributed under the License is distributed on an "AS IS" BASIS,
;;;; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
;;;; See the License for the specific language governing permissions and
;;;; limitations under the License.

;;;; CIDR functions. Parses CIDR specifications to IP address ranges. It
;;;; supports both IPv4 and IPv6.
;;;;
;;;; CIDR tool: // https://www.ipaddressguide.com/cidr


(ns cidr)

(import :java.math.BigInteger)
(import :java.net.InetAddress)
(import :java.nio.ByteBuffer)

(defn
  ^{ :arglists '("(cidr/parse cidr)")
     :doc "Parses CIDR specifications to an IP address range. Supports both IPv4 and IPv6."
     :examples (list
          """
          (cidr/parse "222.192.0.0/11")
          """ ) }

  cidr/parse [cidr]

  (when-not (str/contains? cidr "/")
    (throw (. :VncException :new "Not a valid CIDR format!")))

  (try
    (let [index         (str/index-of cidr "/")
          address-part  (str/subs cidr 0 index)
          network-part  (str/subs cidr (inc index))
          inet-address  (. :InetAddress :getByName address-part)
          prefix-length (long network-part)
          ip4?          (= (count (. inet-address :getAddress)) 4)
          target-size   (if ip4? 4 16)
          mask-buffer   (bytebuf-allocate target-size)]

      (if ip4?
        (bytebuf-put-int! mask-buffer -1I)
        (do (bytebuf-put-long! mask-buffer -1)
            (bytebuf-put-long! mask-buffer -1)))

      (let [mask      (-> (. :BigInteger :new 1 mask-buffer)
                          (. :not)
                          (. :shiftRight prefix-length))
            buffer    (bytebuf (. inet-address :getAddress))
            ip-val    (. :BigInteger :new 1 buffer)
            start-ip  (. ip-val :and mask)
            end-ip    (. start-ip :add (. mask :not))]

        { :cidr     cidr
          :start-ip (. :InetAddress :getByAddress
                       (pad-bytebuf (bytebuf (. start-ip :toByteArray)) target-size))
          :end-ip   (. :InetAddress :getByAddress
                       (pad-bytebuf (bytebuf (. end-ip :toByteArray)) target-size))
        } ))
    (catch :java.net.UnknownHostException ex
           (throw (. :VncException :new (str "Unknown host. CIDR=" cidr) ex)))))


(defn- pad-bytebuf [src size]
  (-> (bytebuf-allocate size)
      (bytebuf-pos! (- size (count src)))
      (bytebuf-put-buf! src 0 size)))
