;; Maven utilities

(defn-
  maven/download-artefact-file
  [uri target-dir target-file]
  (let [target (str target-dir "/" target-file)]
     (println (str "Downloading " uri))
     (println (str "         to " target "..."))
     (->> (io/download uri :binary true)
          (io/spit target))))

(defn-
  ^{ :doc "Parses an artefact like 'com/vaadin:vaadin-client:8.7.2'" }
  maven/parse-artefact
  [artefact repo]
  (let [elements (str/split artefact ":")
        group-id (first elements)
        artefact-id (second elements)
        version (third elements)
        group-path (str/replace-all group-id "." "/")
        file-name-base (str artefact-id "-" version)
        uri (str/join "/" [repo group-path artefact-id version file-name-base])]
    [uri file-name-base]))

(defn-
  maven/download-artefact-by-type
  [artefact repo dir file-suffix]
  (let [elements (maven/parse-artefact artefact repo)
        uri (str (first elements) file-suffix)
        target-file (str (second elements) file-suffix)]
    (maven/download-artefact-file uri dir target-file)))

(defn
  ^{ :arglists '("(maven/download artefact options*)")
     :doc "Downloads an artefact in the format 'group-id:artefact-id:version' from a Maven repository. "
     :examples '(
       "(maven/download \"com.vaadin:vaadin-client:8.7.2\")"
       "(maven/download \"com.vaadin:vaadin-client:8.7.2\" :sources true :pom true)"
       "(maven/download \"com.vaadin:vaadin-client:8.7.2\" :dir \".\")"
       "(maven/download \"com.vaadin:vaadin-client:8.7.2\" :dir \".\" :sources true)"
       "(maven/download \"com.vaadin:vaadin-client:8.7.2\" :dir \".\" :sources true :repo \"https://repo1.maven.org/maven2\")" ) }
  maven/download
  [artefact & options]
  (let [opts (apply hash-map options)
        jar (:jar opts true)
        sources (:sources opts false)
        pom (:pom opts false)
        dir (:dir opts ".")
        repo (:repo opts "https://repo1.maven.org/maven2")]
    (when jar (maven/download-artefact-by-type artefact repo dir ".jar"))
    (when sources (maven/download-artefact-by-type artefact repo dir "-sources.jar"))
    (when pom (maven/download-artefact-by-type artefact repo dir ".pom"))))
