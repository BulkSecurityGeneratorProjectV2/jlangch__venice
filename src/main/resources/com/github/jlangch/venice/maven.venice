;;;;   __    __         _
;;;;   \ \  / /__ _ __ (_) ___ ___
;;;;    \ \/ / _ \ '_ \| |/ __/ _ \
;;;;     \  /  __/ | | | | (_|  __/
;;;;      \/ \___|_| |_|_|\___\___|
;;;;
;;;;
;;;; Copyright 2017-2020 Venice
;;;;
;;;; Licensed under the Apache License, Version 2.0 (the "License");
;;;; you may not use this file except in compliance with the License.
;;;; You may obtain a copy of the License at
;;;;
;;;;     http://www.apache.org/licenses/LICENSE-2.0
;;;;
;;;; Unless required by applicable law or agreed to in writing, software
;;;; distributed under the License is distributed on an "AS IS" BASIS,
;;;; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
;;;; See the License for the specific language governing permissions and
;;;; limitations under the License.

;;;; Maven utilities


(ns maven)


(defn- maven/download-artefact-file [uri target-dir target-file progress-fn]
  (let [target (str target-dir "/" target-file)]
     (when progress-fn
       (println (str "Downloading " uri))
       (println (str "         to " target "...")))
     (->> (io/download uri :binary true :progress-fn progress-fn)
          (io/spit target))))


(defn-
  ^{ :doc "Parses an artefact like 'com/vaadin:vaadin-client:8.7.2'" }

  maven/parse-artefact [artefact repo suffix]

  (let [elements (str/split artefact ":")
        group-id (first elements)
        artefact-id (second elements)
        version (third elements)
        group-path (str/replace-all group-id "." "/")
        file-name (str artefact-id "-" version suffix)
        uri (str/join "/" [repo group-path artefact-id version file-name])]
    [uri file-name]))


(defn
  ^{ :arglists '("(maven/download artefact options*)")
     :doc """
          Downloads an artefact in the format 'group-id:artefact-id:version'
          from a Maven repository.
          """
     :examples '(
          """(maven/download "org.knowm.xchart:xchart:3.6.1")"""
          """(maven/download "org.knowm.xchart:xchart:3.6.1" :sources true :pom true)"""
          """(maven/download "org.knowm.xchart:xchart:3.6.1" :dir ".")"""
          """(maven/download "org.knowm.xchart:xchart:3.6.1" :dir "." :sources true)"""
          """(maven/download "org.knowm.xchart:xchart:3.6.1" :dir "." :sources true :repo "https://repo1.maven.org/maven2")"""
          """(maven/download "org.knowm.xchart:xchart:3.6.1" :dir "." :silent true)""" ) }

  maven/download [artefact & options]

  (let [opts        (apply hash-map options)
        silent      (:silent opts false)
        jar         (:jar opts true)
        sources     (:sources opts false)
        pom         (:pom opts false)
        dir         (:dir opts ".")
        repo        (:repo opts "https://repo1.maven.org/maven2")
        progress-fn (if silent
                      nil
                      (ansi/progress-bar :caption     "            "
                                         :start-msg   "    started "
                                         :end-msg     "            OK"
                                         :failed-msg  "            FAILED"))]
    (when jar
      (let [[uri filename] (maven/parse-artefact artefact repo ".jar")]
        (maven/download-artefact-file uri dir filename progress-fn)))
    (when sources
      (let [[uri filename] (maven/parse-artefact artefact repo "-sources.jar")]
        (maven/download-artefact-file uri dir filename progress-fn)))
    (when pom
      (let [[uri filename] (maven/parse-artefact artefact repo ".pom")]
        (maven/download-artefact-file uri dir filename progress-fn)))))
